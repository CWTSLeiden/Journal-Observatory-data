# Provenance
insert {
    graph sub:docinfo {
        this: a pad:PAD ;
            pad:hasProvenance sub:provenance ;
            pad:hasAssertion sub:assertion ;
            pad:hasDocInfo sub:docinfo .
    }

    graph sub:provenance {
        sub:assertion dcterms:license <https://creativecommons.org/publicdomain/zero/1.0/> ;
            dcterms:created ?date ;
            dcterms:creator <https://openalex.org> .
    }
}
where {
    ?journal a rdfg:Graph .
    ?journal schema:dateModified ?date .
}

# Metadata
insert {
    graph sub:assertion {
        ?journal a ppo:Platform ;
            schema:name ?name .
    }
}
where {
    ?journal a rdfg:Graph .
    ?journal openalex:name ?name .
}

# Organizations
insert {
    graph sub:assertion {
        ?journal dcterms:publisher ?publisher .
        ?publisher a pro:publisher ;
            schema:name ?publisher_name . 
        ?journal dcterms:relation ?society .
        ?society a ppo:Society ;
            schema:name ?society_name ;
            schema:url ?society_url .
    }
}
where {
    ?journal a rdfg:Graph .

    optional { ?journal openalex:host_organization ?publisher } .
    optional { ?journal openalex:publisher_name ?publisher_name} . 
    optional { ?journal openalex:society ?society . bind(str(?society) as ?society_url) } .
    optional { ?society schema:name ?society_name } .
}

# Identifier
insert {
    graph sub:assertion {
        ?journal ?idtype ?pid .
        ?journal ppo:hasOpenalexId ?journal .
    }
}
where {
    ?journal a rdfg:Graph ;
        openalex:external_identifier [ ?idtype ?id ] .
    bind(if(?idtype in (wd:id, ppo:hasFatcatId, ppo:hasOpenalexId), iri(?id), ?id) as ?pid) .
}

# Publication Policy
insert {
    graph sub:assertion {
        ?journal ppo:hasPolicy sub:publicationPolicy .
        sub:publicationPolicy a ppo:PublicationPolicy ;
            ppo:hasArticlePublishingCharges ?apc .
        
        ?apc a ppo:ArticlePublishingCharges ;
            schema:price ?apc_price ;
            schema:priceCurrency "USD" .
    }
}
where {
    ?journal a rdfg:Graph ;
        openalex:apc_usd ?apc_price .
    bind(bnode() as ?apc)
}

insert {
    graph sub:assertion {
        ?journal ppo:hasPolicy sub:publicationPolicy .
        sub:publicationPolicy a ppo:PublicationPolicy ;
            ppo:isOpenAccess ?openaccess .
    }
}
where {
    ?journal a rdfg:Graph ;
        openalex:is_oa ?openaccess .
}
