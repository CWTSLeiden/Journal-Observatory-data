construct {
    this: a job:JobMap ;
        job:hasDataLicense cc0:1.0 ;
        job:hasCreationDate ?date ;
        job:hasDatasource <https://doaj.org> ;
        job:hasAssertion sub:apc ;
        job:hasAssertion sub:metadata .

    sub:apc a job:ArticlePublishingChargesAssertion ;
        job:hasMaxPrice ?price ;
        job:hasPriceCurrency ?currency .

    sub:metadata a job:MetadataAssertion ;
        job:hasKeyword ?keyword ;
        fabio:issn ?pissn ;
        fabio:eIssn ?eissn .
}
where {
    ?journal schema:dateModified ?date .

    ?journal doaj:hasArticlePublishingCharges ?apc .
    optional { ?apc schema:PriceSpecification ?ps } .
    optional { ?ps schema:MaxPrice ?_price } .
    bind(coalesce(?_price, 0) as ?price)
    optional { ?ps schema:priceCurrency ?_currency } .
    bind(if(bound(?_currency), strdt(str(?_currency), xsd:string), ?_) as ?currency)

    optional { ?journal schema:pissn ?pissn . }
    optional { ?journal schema:eissn ?eissn . }
    # optional { ?journal doaj:keyword ?keyword . } 
}
