# Provenance
insert {
    graph sub:head {
        this: a ppo:PAD ;
            ppo:hasAssertion sub:assertion ;
            ppo:hasProvenance sub:provenance .
    }

    graph sub:provenance {
        sub:assertion cc:license <https://creativecommons.org/publicdomain/zero/1.0/> ;
            dcterms:created ?date ;
            dcterms:creator <https://openalex.org> .
    }
}
where {
    ?journal a rdfg:Graph .
    ?journal schema:dateModified ?date .
}

# Metadata Assertion
insert {
    graph sub:assertion {
        ?journal a ppo:Platform ;
            schema:name ?name .
    }
}
where {
    ?journal a rdfg:Graph .
    ?journal openalex:name ?name .
}

# Publisher Assertion
insert {
    graph sub:assertion {
        ?journal dcterms:publisher [ schema:name ?publisher ] .
    }
}
where {
    ?journal a rdfg:Graph .
    ?journal openalex:publisher_name ?publisher . 
}

# Identifier Assertion
insert {
    graph sub:assertion {
        ?journal ?hasissn ?issn ;
            openalex:venueId ?journal .
    }
}
where {
    ?journal a rdfg:Graph .
    optional {
        ?journal ?hasissn ?issn . 
        filter ( ?hasissn in (prism:issn, fabio:hasIssnL) )
    } .
}
