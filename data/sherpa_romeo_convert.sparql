# Jobmap
insert {
    graph sub:head {
        this: a ppo:PAD ;
        ppo:hasAssertion sub:assertion ;
        ppo:hasProvenance sub:provenance .
    }

    graph sub:provenance {
        sub:assertion cc:license <https://creativecommons.org/licenses/by-nc-nd/4.0/> ;
            dcterms:created ?date ;
            dcterms:creator <https://v2.sherpa.ac.uk/romeo> .
    }
}
where {
    ?journal a rdfg:Graph .
    ?journal romeo:hasMetadata [ schema:dateCreated ?date ] .
}

# Metadata
insert {
    graph sub:assertion {
        ?romeo_id a ppo:Platform ;
            schema:name ?journal_title ;
            schema:url ?journal_url .
    }
}
where {
    ?journal a rdfg:Graph .
    ?journal romeo:hasMetadata ?romeo_id .
    optional { ?journal romeo:hasTitle [ romeo:title ?journal_title ] . }
    optional { ?journal schema:url ?journal_url . }
}

# Identifier
insert {
    graph sub:assertion {
        ?romeo_id romeo:id ?romeo_id ;
            prism:issn ?pissn ;
            prism:eIssn ?eissn .
    }
}
where {
    ?journal a rdfg:Graph .
    ?journal romeo:hasMetadata ?romeo_id .
    optional { ?journal romeo:hasIssns ?issns . }
    optional { ?issns prism:issn ?pissn ; romeo:issn_type "print" . }
    optional { ?issns prism:issn ?eissn ; romeo:issn_type "electronic" . }
}

# Organization
insert {
    graph sub:assertion {
        ?romeo_id ppo:hasOrganization ?publisher .
        ?publisher a pro:publisher ; 
            schema:name ?publisher_name ;
            schema:addressCountry ?publisher_country ;
            schema:url ?publisher_url .
    }
}
where {
    ?journal a rdfg:Graph .
    ?journal romeo:hasMetadata ?romeo_id .
    ?journal romeo:hasPublisher [ romeo:Publisher ?publisher ] .
    ?publisher romeo:hasPublisherName [ romeo:PublisherName ?publisher_name ] .
    optional { ?publisher schema:url ?publisher_url . }
    optional { ?publisher romeo:publisherCountry ?publisher_country . }
}

insert {
    graph ?graph {
        ?policy dcterms:license ?license
    }
}
where {
    graph ?graph {
        ?policy romeo:hasPermittedLicense [ romeo:PermittedLicense ?permittedlicense ] .
        optional { ?permittedlicense romeo:PermittedLicenseVersion ?licenseversion . } .
        bind(if(?permittedlicense in ("cc0", "cc_public_domain"), concat("publicdomain/zero/", coalesce(?licenseversion, "1.0")), ?_) as ?license_str)
        bind(if(?permittedlicense = "cc_by", concat("licenses/by/", coalesce(?licenseversion, "4.0")), ?_) as ?license_str)
        bind(if(?permittedlicense = "cc_by_nc", concat("licenses/by-nc/", coalesce(?licenseversion, "4.0")), ?_) as ?license_str)
        bind(if(?permittedlicense = "cc_by_nc_nd", concat("licenses/by-nc-nd/", coalesce(?licenseversion, "4.0")), ?_) as ?license_str)
        bind(if(?permittedlicense = "cc_by_nc_sa", concat("licenses/by-nc-sa/", coalesce(?licenseversion, "4.0")), ?_) as ?license_str)
        bind(if(?permittedlicense = "cc_by_nd", concat("licenses/by-nd/", coalesce(?licenseversion, "4.0")), ?_) as ?license_str)
        bind(if(?permittedlicense = "cc_by_sa", concat("licenses/by-sa/", coalesce(?licenseversion, "4.0")), ?_) as ?license_str)
        bind(if(bound(?license_str), iri(concat("https://creativecommons.org/", encode_for_uri(?license_str))), ?_) as ?license)
    }
}

insert {
    graph ?graph {
        ?policy ppo:hasEmbargo ?embargoduration
    }
}
where {
    graph ?graph {
        ?policy romeo:hasEmbargo [ romeo:embargoAmount ?embargoamount ;
                romeo:embargoUnits ?embargounit ] .
        bind(if(?embargounit="weeks", ?embargoamount * 7, ?embargoamount) as ?embargoamount)
        bind(if(?embargounit="weeks", "days", ?embargounit) as ?embargounit)
        bind(strdt(concat("P", str(?embargoamount), ucase(substr(?embargounit, 1, 1))), xsd:duration) as ?embargoduration)
    }
}

insert {
    graph ?graph {
        ?policy ppo:hasCopyrightOwner ?copyright_owner .
        ?copyright_owner a ?copyright_owner_type .
    }
}
where {
    graph ?graph {
        ?journal a rdfg:Graph .
        ?journal romeo:hasMetadata ?romeo_id .
        ?policy romeo:copyrightOwner ?_copyright_owner
        optional { ?journal romeo:hasPublisherPolicy ?publisher_policy .
            ?publisher_policy romeo:hasPermittedOA ?policy .
            ?journal romeo:hasPublisher [ romeo:Publisher ?publisher ] .
            ?publisher_policy romeo:PublisherID ?publisherid .
            ?publisher romeo:PublisherID ?publisherid .
        }
        bind(sub:author as ?author)
        bind(if(bound(?publisher), ?publisher, sub:publisher) as ?publisher)
        bind(sub:society as ?society)
        bind(sub:institution as ?institution)

        bind(if(contains(?_copyright_owner, "authors"), ?author, ?_) as ?copyright_owner) .
        bind(if(contains(?_copyright_owner, "authors"), pro:author, ?_) as ?copyright_owner_type) .
        bind(if(contains(?_copyright_owner, "journal"), ?romeo_id, ?_) as ?copyright_owner) .
        bind(if(contains(?_copyright_owner, "journal"), pro:journal, ?_) as ?copyright_owner_type) .
        bind(if(contains(?_copyright_owner, "publisher"), ?publisher, ?_) as ?copyright_owner) .
        bind(if(contains(?_copyright_owner, "publisher"), pro:publisher, ?_) as ?copyright_owner_type) .
        bind(if(contains(?_copyright_owner, "society"), ?society, ?_) as ?copyright_owner) .
        bind(if(contains(?_copyright_owner, "society"), ppo:society, ?_) as ?copyright_owner_type) .
        bind(if(contains(?_copyright_owner, "institution"), ?institution, ?_) as ?copyright_owner) .
        bind(if(contains(?_copyright_owner, "institution"), ppo:authors-institution, ?_) as ?copyright_owner_type) .
    }
}


# Dissemination Policy
insert {
    graph sub:assertion {
        ?romeo_id ppo:hasPolicy ?disseminationpolicy .
        ?disseminationpolicy a ppo:DisseminationPolicy ;
            ppo:openAccessAllowed ?oaallowed ;
            ppo:hasOpenAccessFee ?oafee ;
            ppo:disseminatesVersion ppo:publishedVersion ;
            dcterms:license ?license ;
            ppo:hasCopyrightOwner ?copyright_owner ;
            ppo:embargo ?embargoduration .
        ?copyright_owner a ?copyright_owner_type .
    }
}
where {
    ?journal a rdfg:Graph .
    ?journal romeo:hasMetadata ?romeo_id .
    ?journal romeo:hasPublisherPolicy ?publisher_policy .
    ?publisher_policy romeo:hasPermittedOA ?policy .
    ?policy romeo:articleVersion "published" .
    bind(bnode() as ?disseminationpolicy)

    optional { ?policy romeo:oaProhibited ?oaprohibited
        values (?oaprohibited ?oaallowed) {
            ("yes" false)
            ("no" true)
            (undef true)
        }
    }
    optional { ?policy romeo:hasAdditionalFee ?_oafee
        values (?_oafee ?oafee) {
            ("no" false)
            ("yes" true)
        }
    }
    optional { ?policy ppo:hasCopyrightOwner ?copyright_owner .
        ?copyright_owner a ?copyright_owner_type .
    } .
    optional { ?policy dcterms:license ?license } .
    optional { ?policy ppo:hasEmbargo ?embargoduration } .
}

# Publication Elsewhere Policy
insert {
    graph sub:assertion {
        ?romeo_id ppo:hasPolicy ?publicationelsewhereprohibited .
        ?publicationelsewhereprohibited a ppo:PublicationElsewhereProhibited .

        ?romeo_id ppo:hasPolicy ?publicationelsewhereallowed .
        ?publicationelsewhereallowed a ppo:PublicationElsewhereAllowed ;
            dcterms:creator ?publisher ;
            ppo:appliesToVersion ?articleversion ;
            dcterms:license ?license ;
            ppo:hasCopyrightOwner ?copyright_owner ;
            ppo:condition ?condition ;
            ppo:location ?location ;
            ppo:embargo ?embargoduration .
        ?copyright_owner a ?copyright_owner_type .
    }
}
where {
    ?journal a rdfg:Graph .
    ?journal romeo:hasMetadata ?romeo_id .
    bind(bnode() as ?publicationelsewhereprohibited)
    ?journal romeo:hasPublisherPolicy ?publisher_policy .
    ?publisher_policy romeo:hasPermittedOA ?policy .
    bind(bnode() as ?publicationelsewhereallowed)

    optional { ?journal romeo:hasPublisher [ romeo:Publisher ?publisher ] .
        ?publisher_policy romeo:PublisherID ?publisherid .
        ?publisher romeo:PublisherID ?publisherid .
    }
    optional { ?policy romeo:articleVersion ?_articleversion .
        values (?_articleversion ?articleversion) {
            ("submitted" ppo:submittedVersion)
            ("accepted" ppo:acceptedVersion)
            ("published" ppo:publishedVersion)
        }
    } .
    optional { ?policy ppo:hasCopyrightOwner ?copyright_owner .
        ?copyright_owner a ?copyright_owner_type .
    } .
    optional { ?policy romeo:condition ?condition } .
    optional { ?policy romeo:hasPermittedLocation [ romeo:PermittedLocation ?location ] } .
    optional { ?policy ppo:hasLicense ?license } .
    optional { ?policy ppo:hasEmbargo ?embargoduration } .
}
