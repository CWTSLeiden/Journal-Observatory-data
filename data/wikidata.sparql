insert {
    graph sub:docinfo {
        this: a pad:PAD ;
            pad:hasProvenance sub:provenance ;
            pad:hasAssertion sub:assertion .
    }

    graph sub:provenance {
        sub:assertion dcterms:license <https://creativecommons.org/publicdomain/zero/1.0/> ;
            dcterms:creator <https://www.wikidata.org> ;
            dcterms:created ?date .
    }

    graph sub:assertion {
        ?journal a ppo:Platform ;
            schema:name ?title ;
            schema:url ?platform_url ;
            ?pidtype ?pid ;
            ppo:hasWikidataId ?journal ;
            ppo:hasPaywall ?paywall ;
            ppo:hasOrganization ?publisher .
        ?publisher a pro:publisher .
    }
}
WHERE {
    SERVICE <$sparql_endpoint> {
        ?journal wdt:P31 wd:Q5633421 .
        ?journal schema1:dateModified ?date .

        optional { ?journal wdt:P1476 ?title } .
        optional { ?journal wdt:P856 ?platform_url } .
        optional { ?journal ?idtype ?id .
            values (?idtype ?pidtype) {
                (wdt:P236 prism:issn)
                (wdt:P7363 fabio:hasIssnL)
                (wdt:P1662 ppo:hasDoiPrefix)
                (wdt:P10283 ppo:hasOpenalexId)
                (wdt:P8375 ppo:hasCrossrefId)
                (wdt:P6180 ppo:hasDimensionsId)
                (wdt:P1156 ppo:hasScopusId)
                (wdt:P8608 ppo:hasFatcatId)
                (wdt:P6366 ppo:hasMagId)
            } .
            bind(if(?idtype in (wdt:P10283), iri(concat("https://openalex.org/", ?id)), ?id) as ?pid) .
        }
        optional {
            ?journal wdt:P123 ?publisher .
            ?publisher wdt:P31 wd:Q2085381 .
        } .
        optional {
            ?journal wdt:6954 ?onlineaccessstatus .
            values ( ?onlineaccessstatus ?paywall ) {
                (wd:Q910845 true)
                (wd:Q24707952 false)
                (wd:Q232932 false)
            }
        }
        filter (?journal = <$journal_id>)
    }
}
