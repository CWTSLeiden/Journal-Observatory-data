insert {
    graph sub:docinfo {
        this: a pad:PAD ;
            pad:hasProvenance sub:provenance ;
            pad:hasAssertion sub:assertion ;
            pad:hasDocInfo sub:docinfo .
        sub:provenance a pad:Provenance .
        sub:assertion a pad:Assertion .
        sub:docinfo a pad:DocInfo .
    }

    graph sub:provenance {
        sub:assertion dcterms:license <https://creativecommons.org/publicdomain/zero/1.0/> ;
            dcterms:creator <https://www.wikidata.org> ;
            dcterms:created ?date .
    }

    graph sub:assertion {
        ?journal a scpo:Platform ;
            schema:name ?title ;
            schema:url ?platform_url_str ;
            ?pidtype ?pid ;
            wd:id ?journal ;
            scpo:isOpenAccess ?openaccess ;
            dcterms:publisher ?publisher .
        ?publisher a pro:publisher ;
            schema:name ?publisher_name ;
            schema:url ?publisher_url_str .
    }
}
WHERE {
    SERVICE <$sparql_endpoint> {
        ?journal wdt:P31 wd:Q5633421 .
        ?journal schema1:dateModified ?date .

        optional { ?journal wdt:P1476 ?title } .
        optional { ?journal wdt:P856 ?platform_url . bind(str(?platform_url) as ?platform_url_str) . } .
        optional { ?journal ?idtype ?id .
            values (?idtype ?pidtype) {
                (wdt:P236 prism:issn)
                (wdt:P7363 fabio:hasIssnL)
                (wdt:P10283 scpo:hasOpenalexId)
                (wdt:P8375 scpo:hasCrossrefId)
                (wdt:P6180 scpo:hasDimensionsId)
                (wdt:P1156 scpo:hasScopusId)
                (wdt:P8608 scpo:hasFatcatId)
                (wdt:P6366 scpo:hasMagId)
            } .
            bind(if(?idtype in (wdt:P10283), iri(concat("https://openalex.org/", ?id)), ?id) as ?pid) .
        }
        optional {
            ?journal wdt:P123 ?publisher .
            ?publisher wdt:P31 wd:Q2085381 .
            optional { ?publisher rdfs:label ?publisher_name . filter(langMatches(lang(?publisher_name), "en"))} .
            optional { ?publisher wdt:P856 ?publisher_url  . bind(str(?publisher_url) as ?publisher_url_str) . } .
        } .
        optional {
            ?journal wdt:6954 ?onlineaccessstatus .
            values ( ?onlineaccessstatus ?openaccess ) {
                (wd:Q910845 false)
                (wd:Q24707952 true)
                (wd:Q232932 true)
            }
        }
        filter (?journal = <$journal_id>)
    }
}
